apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "com.dab.zx"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 2
        versionName "1.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {

    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            if (variant.buildType.name == 'release') {
                variant.mergedFlavor.versionCode = getVersionCode(false)
                // release
                outputFileName = "Zillions-of-enemy-X_v${variant.versionCode}.apk"
            } else {
                variant.mergedFlavor.versionCode = getVersionCode(true)
                // debug
                outputFileName = "Zillions-of-enemy-X_v${variant.versionCode}.apk"
            }
        }
    }
    dataBinding {
        enabled true
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    kapt "com.android.databinding:compiler:$android_plugin_version"
    implementation 'com.android.support:multidex:1.0.2'
    // Support
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support:support-v4:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'
    // Google
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    // Anko
    implementation 'org.jetbrains.anko:anko:0.10.0'
    // Rx
    implementation 'io.reactivex.rxjava2:rxjava:2.0.8'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.github.MFlisar:RxBus2:0.1'
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'
    // Retrofit
    implementation 'com.squareup.retrofit2:converter-gson:2.0.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    // FileDownloader
    implementation 'com.liulishuo.filedownloader:library:1.4.1'
    // OKHttp
    implementation 'com.squareup.okhttp3:okhttp:3.7.0'
    // SwipeBackLayout
    implementation 'me.imid.swipebacklayout.lib:library:1.1.0'
    // Banner
    implementation 'com.youth.banner:banner:1.4.5'
//    // PtrLayout
//    implementation 'com.chanven.lib:cptr:1.1.0'
}
repositories {
    mavenCentral()
}

// 获取 version code
static def getVersionCode(boolean isDebug) {
    if (isDebug) {
        return Integer.parseInt(new Date().format("yyMMddHHmm"))
    }
    return getRevisionNumber()
}

// 获取修订版本 git 提交次数
static def getRevisionNumber() {
    Process process = "git rev-list --count HEAD".execute()
    process.waitFor()
    return process.getText().toInteger()
}

